.SYNTAX program

program = $st '.END' .,

st      = .ID           .LABEL *
          '=' ex1 ';'   .OUT('R') .,

ex1     = ex2 $('|'     .OUT('BT ' *1)
                ex2)
                        .LABEL *1 .,

ex2     =  ( ex3        .OUT('BF ' *1)
           / output)
          $( ex3        .OUT('BE')
           / output)
                        .LABEL *1 .,

ex3     = .ID           .OUT('CLL '*)
        / .STRING       .OUT('TST '*)
        / '.ID'         .OUT('ID')
        / '.NUMBER'     .OUT('NUM')
        / '.STRING'     .OUT('SR')
        / '(' ex1 ')'
        / '.EMPTY'      .OUT('SET')
        / '*'           .LABEL *1
          ex3           .OUT('BT ' *1)
                        .OUT('SET') .,

output  = ( '<' $out1 '>'
          / '.LABEL'    .OUT('LB')
            out1)
                        .OUT('OUT') .,

out1    = '%1'          .OUT('GN1')
        / '%2'          .OUT('GN2')
        / '%'           .OUT('CI')
        / .STRING       .OUT('CL '*) .,

.END
