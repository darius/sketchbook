program = *st .EOF;

st      = .ID            .LABEL %
          '=' choice ';' <'R'>;

choice  = chain *('|'    <'BT ' %1>
                  chain)
                         .LABEL %1;

chain   =  ( maybe       <'BF ' %1>
           | surely)
          *( maybe       <'BE'>
           | surely)
                         .LABEL %1;

maybe   = .ID            <'CLL '%>
        | .STRING        <'TST '%>
        | '.EOF'         <'EOF'>
        | '.ID'          <'ID'>
        | '.NUMBER'      <'NUM'>
        | '.STRING'      <'SR'>
        | '(' choice ')';

surely  = '.EMPTY'       <'TST ' .Q .Q>
        | '*'            .LABEL %1
          maybe          <'BTT '%1>
        | ( '<' *out '>'
          | '.LABEL'     <'LB'>
            out)
                         <'OUT'>;

out     = '%1'           <'GN1'>
        | '%2'           <'GN2'>
        | '%'            <'CI'>
        | '.Q'           <'CQ'>
        | .STRING        <'CL '%>;
